Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def pocketsphinx_dir = properties.getProperty('pocketsphinx.dir')
def sphinxbase_dir = properties.getProperty('sphinxbase.dir')

buildscript {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        mavenCentral()
        jcenter() // Warning: this repository is going to shut down soon
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

apply plugin: 'com.android.library'

android {
    namespace 'edu.cmu.pocketsphinx'
    compileSdk 29
	ndkVersion "25.2.9519653"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 5
        versionName "5prealpha"
        setProperty("archivesBaseName", "pocketsphinx-android-$versionName")
        externalNativeBuild {
            cmake {
                arguments "-DPOCKETSPHINX_DIR=$pocketsphinx_dir", "-DSPHINXBASE_DIR=$sphinxbase_dir"
            }
        }
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }
    sourceSets {
         main {
             java.srcDirs = ['src/main/java', 'build/generated-src', '.apt_generated']
         }
    }
    externalNativeBuild {
        cmake {
             path "CMakeLists.txt"
        }
    }
    // Configure only for each module that uses Java 8
    // language features (either in its source code or
    // through dependencies).
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


task swig {
    doFirst {
        assert file("$sphinxbase_dir/include").exists()
        assert file("$pocketsphinx_dir/include").exists()
    }
    doLast {
        mkdir 'build/generated-src/java'
        mkdir 'build/generated-src/cpp'
        exec {
            commandLine 'swig',
                "-I$sphinxbase_dir/include", "-I$sphinxbase_dir/swig",
                "-java", "-package", "edu.cmu.pocketsphinx",
                "-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/sphinxbase_wrap.c",
                "$sphinxbase_dir/swig/sphinxbase.i"
        }
        exec {
            commandLine 'swig',
                "-I$sphinxbase_dir/swig",
                "-I$pocketsphinx_dir/include",
                "-I$pocketsphinx_dir/swig",
                "-java", "-package", "edu.cmu.pocketsphinx",
                "-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/pocketsphinx_wrap.c",
                "$pocketsphinx_dir/swig/pocketsphinx.i"
        }
    }
}

preBuild.dependsOn swig
